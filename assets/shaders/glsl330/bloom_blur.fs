#version 330

in vec2 fragTexCoord;
in vec4 fragColor;

uniform sampler2D texture0;
uniform int horizontal;
uniform vec2 renderSize; // Size of the texture being blurred (e.g. 320, 180)

out vec4 finalColor;

// Gaussian weights for a 9-tap filter
const float weight[5] = float[](0.227027, 0.1945946, 0.1216216, 0.054054, 0.016216);

void main() {
    vec2 tex_offset = 1.0 / renderSize; // Size of a single texel
    vec3 result = texture(texture0, fragTexCoord).rgb * weight[0];
    
    if (horizontal != 0) {
        for (int i = 1; i < 5; ++i) {
            result += texture(texture0, fragTexCoord + vec2(tex_offset.x * float(i), 0.0)).rgb * weight[i];
            result += texture(texture0, fragTexCoord - vec2(tex_offset.x * float(i), 0.0)).rgb * weight[i];
        }
    } else {
        for (int i = 1; i < 5; ++i) {
            result += texture(texture0, fragTexCoord + vec2(0.0, tex_offset.y * float(i))).rgb * weight[i];
            result += texture(texture0, fragTexCoord - vec2(0.0, tex_offset.y * float(i))).rgb * weight[i];
        }
    }
    
    finalColor = vec4(result, 1.0);
}
